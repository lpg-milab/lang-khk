Alphabet
! The alphabet should contain all symbols which are used in the grammar.
! Symbols consist of strings of utf-8 characters. Reserved words and white-space
! need to be quoted using %.
а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я 

!нөхцөл зааглагч
%>:0 

!урт ий эгшиг
%{Ы%}:0 %{Ы%}:и %{Ы%}:й %{Ы%}:ы 

%{Ъ%}:ъ

!балархай эгшигүүд
%{а%}:а %{э%}:э %{о%}:0 %{о%}:о %{ө%}:ө %{и%}:и

%{1%}:0 %{2%}:0 %{3%}:0 %{4%}:0
;


Sets
C = б в г д ж з к л м н п р с т ф х ц ч ш щ е ё ю я ;
V = а и о ө у ү э ;
GG = э ү ө ; 
RR = а о у я ;
MN = м н г л б в р ;
ZZ = д ж з ц с т х ч ш ;
ZH = д ж з ц с т ч ш ;
ZS = д ж з ц т ч ш х ;
ZD = ж з ц т ч ш ;
ZT = д з ц с т х; 
TT = ч ш ж к ;
DP = г с р в ;
SS = ч ш ж г ;				
QL = б в д з л м н п р с т ф х ц е ё ю я ;
FK = ю я е ё ;
WW = м н л в ;
OO = а у я ;
FF = э ү и е ю ;
YY = а и о ө у ү э й ;
BB = э ү е ю ө ;
II1 = д ж з ц с т ч ш ;
JJ = г н ;
PP = с т м р ;
MM = м н г л б в ;
HH = д л ;
FI = э ү ;
OI = а и ;
BI = а и о ө у ү э ь ;
DS = д с ;

VV = а о ө э ;
YY = у ү ;
II = и й ы ;


ND = м н г л б в р а о э ө и й ы у ү я е ё ;

Rules 

"Олон нөхцөл дарааллаж орсон тохиолдолд эгшиг гээх"
VV:0 <=>
			:ND %>: х _ н 	%>: [V:| YY: | II:]+ ;
			%>: г _ 	%>: [VV: | YY:]+  ;
			%>: л г _ 	%>: [VV: | YY:]+ ;
			%>: (V:) з н _ %>: [VV: | YY:]+ ;
			%>: (C:) VV: VV: т _ х %>: [ VV: | YY: | MN: | ZZ: ]+  ;
			:ND %>: т _ л 	%>: [VV:]+ ; 
			:ND %>: с _ н 	%>: [VV: | II:]+ ;
			%>: г с _ д %>: [V: | II:]+ ;
			:ND %>: д _ г 	%>: [VV: | II:]+ ;	

			%>: _ д %>: [V:^2 | FK:^2 ] ;			
			

"Олон нөхцөл дарааллаж орсон тохиолдолд и эгшиг гээх"
и:0 <=>
		%>: ч _ л %>: (:VV) н ;

"чих дээр хлаар залгавал нэг х гээгдэнэ"
х:0 <=> %>: ч и _ %>: х л VV: VV: р ;

!!€ шархт>сан>аа
!!€ шархт0сан0аа

!!€ намна>сан>аа
!!€ намна0с0н0аа

!!€ асуу>зна>аа
!!€ асуу0зн00аа

!!€ үзүүл>знэ>ээтэх>ээ
!!€ үзүүл0зн00ээт0х0ээ

!!€ алдарш>зна>уул
!!€ алдарш0зн00уул

!!€ алдарш>уул>даг>ийн
!!€ алдарш0уул0д0г0ийн

"Олон нөхцөл дарааллаж орсон тохиолдолд и эгшиг гээх: Үйл үг бүтээх дагавар"
и:0 <=> %>: ч _ л 	%>: VV: н ; 

!!€ асуу>чил>ан
!!€ асуу0ч0л0ан

"Үгийн эцсийн а эгшиг гээгдэх. осор 28.2"
а:0 <=> :C _ %>: [ы:|и:] (й:) (:C) ;
		:C _ %>: :Vx :Vx  (:C) ;
		where Vx in (а о у) ;

!!€ дарга>ын
!!€ дарг00ы0н

!!€ дарга>аар
!!€ дарг00аар

"Үгийн эцсийн о эгшиг гээгдэх"
о:0 <=> :C _ %>: [ы:|и:] (й:) (:C) ;
		:C _ %>: :Vx :Vx  (:C) ;
		where Vx in (о у) ;

"Үгийн эцсийн ө эгшиг гээгдэх"
ө:0 <=> :C _ %>: и: й: (:C) ;
		:C _ %>: :Vx :Vx  (:C) ;
		where Vx in (ө ү) ;

"Үгийн эцсийн э эгшиг гээгдэх"
э:0 <=> :C _ %>: и: й: (:C) ;
		:C _ %>: :Vx :Vx  (:C) ;
		where Vx in (э ү) ;

!!€ илбэ>{Ү}{Ү}р
!!€ илб00ээр


"Эцсийн гийгүүлэгчийн өмнөх балархай 'а' эгшиг гээгдэх осор 28.1"
%{а%}:0 <=>	_ :C %>: VV: (VV:) ;
			_ :C %>: YY: (YY:) ;
			_ :C %>: ы: ;
			_ :C %>: и: й: ;
			_ :C %>: :V :C ;

!!€ ар{а}л>{Ы}{Ы}н
!!€ ар0л0ы0н

"Эцсийн гийгүүлэгчийн өмнөх балархай 'э' эгшиг гээгдэх осор 28.1"
%{э%}:0 <=>	_ :C %>: VV: (VV:) ;
			_ :C %>: YY: (YY:) ;
			_ :C %>: ы: ;
			_ :C %>: и: й: ;
			_ :C %>: :V :C ;

"Эцсийн гийгүүлэгчийн өмнөх балархай 'о' эгшиг гээгдэх осор 28.1"
%{о%}:0 <=>	_ :C %>: VV: (VV:) ;
			_ :C %>: YY: (YY:) ;
			_ :C %>: ы: ;
			_ :C %>: и: й: ;
			_ :C %>: :V :C ;

!!€ бод{о}л>{Ы}{Ы}н
!!€ бод0л0ы0н

!!€ бод{о}л>{Ү}{Ү}с
!!€ бод0л0оос

"Эцсийн гийгүүлэгчийн өмнөх балархай 'ө' эгшиг гээгдэх осор 28.1"
%{ө%}:0 <=>	_ :C %>: VV: (VV:) ;
			_ :C %>: YY: (YY:) ;
			_ :C %>: ы: ;
			_ :C %>: и: й: ;
			_ :C %>: :V :C ;

"Эцсийн гийгүүлэгчийн өмнөх балархай 'и' эгшиг гээгдэх осор 28.1"
%{и%}:0 <=>	_ :C %>: VV: (VV:) ;
			_ :C %>: YY: (YY:) ;
			_ :C %>: ы: ;
			_ :C %>: и: й: ;
			_ :C %>: :V :C ;

!!€ шувуучин>ы
!!€ шувууч0н0ы

!!€ бороо>н{Ы}{Ы}>х{Ү}н>{Ы}{Ы}
!!€ бороо>ны00х0н0ы0


"Зөөлний тэмдэгээр төгссөн үгэнд урт И эгшиг залгахад зөөлний тэмдэг И болно"
ь:и <=> C _ %>: [VV:]+ ;
		C _ %>: [й: | и:] ;

"Зөөлний тэмдэг и болох дүрэм: ь:и (1.7)"
ь:и <=> _ %>: [:а|:о|:у] ; 	! осор 32.1
		_ %>: :э ;	!  осор 32.1 гадаад үгэнд: вексель
		_ %>: :MN ; ! осор 32.2
		_ %>: :х ; ! осор 32.3
		ZZ _ %>: :ZH ; ! осор 32.4
		![х|т] _ %>: [:д|:т] ;
	except
		_ %>: г ү й ; ! осор 33.3. 


!!€ ань>р{У}{У}н
!!€ ани0руун

!!€ морь>гүй
!!€ морь>гүй

!!€ хууль>гүй
!!€ хууль>гүй

"руу луу болох"
р:л <=>	р %>: % _ YY: YY: ;
